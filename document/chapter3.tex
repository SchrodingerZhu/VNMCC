\chapter{Journey to the Clash Language}
\lettrine{C}{lash} will be our main language to write the CPU. Clash supports most of Haskell syntax, yet it cannot support some advanced features such as GADT pattern matching. To see the full list of limitations, please check its \href{http://hackage.haskell.org/package/clash-prelude-1.2.0/docs/Clash-Tutorial.html}{official tutorial}

(http://hackage.haskell.org/package/clash-prelude-1.2.0/docs/Clash-Tutorial.html). It will also be a great idea to go through the troubleshooting part if you face some difficulties later.

\section{Define Circuits}
You can simply write a circuit in the way of writing a Haskell function:
\begin{minted}{Haskell}
module Example where
orGate :: Bool -> Bool -> Bool
orGate = (||)
\end{minted}
How to generate a verilog module from the code? If your function is named as \mintinline{haskell}{topEntity}, just load the clash.clashi on your own or using stack and then input \mintinline{haskell}{:verilog Example} in the REPL, then the outputs are ready at the \mintinline{verilog}{verilog} subdirectory under your working directory. 

However, in most cases, you need to write a special annotation for the function:
\begin{minted}{Haskell}
{-# ANN orGate
  ( Synthesize
    { t_name = "OrGate"
    , t_inputs = 
        [ PortName "X"
        , PortName "Y"
        ]
    , t_output = 
        PortName "RESULT" 
    }
  )
#-}
\end{minted}
You can customize the names of the ports or the whole module. Another fantastic thing you can do is to create a test bench for your circuits. As we are not going to use Clash to generate test benches, it is up to you to investigate. Here is \href{http://hackage.haskell.org/package/clash-prelude-1.2.1/docs/Clash-Annotations-TopEntity.html#v:TestBench}{the link}:

\begin{minted}[breaklines]{bash}
http://hackage.haskell.org/package/clash-prelude-1.2.1/docs/
Clash-Annotations-TopEntity.html#v:TestBench
\end{minted}

Here is another example to demonstrate how to handle product ports.
\begin{minted}{Haskell}
{-# ANN someGates
  (Synthesize
     {  t_name = "SomeGates"
     ,  t_inputs = 
        [  PortName "X"
        ,  PortProduct "IN" 
             [  PortName "Y"
             ,  PortName "Z"
             ]
         ]
     ,  t_output = PortProduct "OUT" 
        [  PortName "0"
        ,  PortName "1"
        ,  PortName "2"
        ]
     }
  )
#-}
\end{minted}
This annotation can be used with functions in the form of 
\begin{minted}{haskell}
someGate :: x -> (y, z) -> (o0, o1, o2)
\end{minted} 

\section{Useful Types}
\subsection{Bit and BitVector}
Bit is just bit and BitVector is just a statically sized vector of bits. As a single Bit and Bool are quite the same, \mintinline{haskell}{Clash.Prelude} provides some handy functions for us to convert them from each other.
\begin{minted}{haskell}
boolToBit :: Bool -> Bit
bitToBool :: Bit -> Bool
boolToBV :: KnownNat n => Bool -> BitVector (n + 1)
\end{minted}
BitVector can be sliced and indexed, the following code shows some examples:
\begin{minted}[breaklines]{haskell}
(!) :: (BitPack a, Enum i) => a -> i -> Bit
-- ^ BitVector is within the class of BitPack, so we can use (!) operator to fetch the value

vec ! 0
-- ^ fetch the lowest bit

slice :: (BitPack a, BitSize a ~ ((m + 1) + i)) 
    => SNat m -> SNat n -> a -> BitVector ((m + 1) - n)
-- ^ fetch a slice from the the bit vector
-- ^ because the vector is defined as a dependent type
-- ^ this interface is a bit terrifying, but it is handy to use

slice d31 d20 vec
-- ^ slice from index 31 to index 20
\end{minted}
As you can see, \mintinline{haskell}{d0} to \mintinline{haskell}{d1024} are predefined literals for static natural numbers, you can use them for the vector index.

How about the bitwise operations? There is a whole set of operators and functions.
\begin{minted}{haskell}
(.|.) :: Bits a => a -> a -> a          -- ^ bitwise or
(.&.) :: Bits a => a -> a -> a          -- ^ bitwise and
xor :: Bits a => a -> a -> a            -- ^ bitwise xor
complement :: Bits a => a -> a          -- ^ bitwise not
shiftR :: Bits a => a -> Int -> a       -- ^ bitwise shift
shiftL :: Bits a => a -> Int -> a       -- ^ bitwise shift
unsafeShiftR :: Bits a => a -> Int -> a -- ^ bitwise shift
\end{minted}
\subsection{Sized Integers}
There are mainly two types of sized integers: \mintinline{haskell}{Unsigned (n :: Nat)} and
\mintinline{haskell}{Signed (n :: Nat)}. Most bitwise operations can also be applied to sized integers, but please notice that for right shifting, the normal version takes care of the sign bit and the unsafe version just do the logical shifting.

It is also possible to extend sized integers and BitVector,
\begin{minted}{haskell}
extend :: (Resize f, KnownNat a, KnownNat b) => f a -> f (b + a)
\end{minted}
It is very handy that extensions will handle the changing of the sign bits automatically.

Although BitVector and sized integers are very similar, you cannot treat them as the same thing. However, if you need to convert the types, you can use the following functions:
\begin{minted}{haskell}
pack :: BitPack a => a -> BitVector (BitSize a)
unpack :: BitPack a => BitVector (BitSize a) -> a
\end{minted}
\subsection{Sized Vector}
Another important type is sized vector: \mintinline{haskell}{Vec :: Nat -> * -> *}. In fact, the memory blocks that we are going to use are just wrappers around this type (\mintinline{haskell}{Vec 512 (BitVector 32)}).
To get the value at a specific index, you can use the following function:
\begin{minted}{haskell}
(!!) :: (KnownNat n, Enum i) => Vec n a -> i -> a
\end{minted}
To update the data, you can use this function:
\begin{minted}{haskell}
replace :: (KnownNat n, Enum i) => i -> a -> Vec n a -> Vec n a
\end{minted}
Notice that, consecutive update at the single clock (which is really a bad decision) may make Clash fail to synthesize the circuit.
Sometimes we need to provide an initial value for the RAM, then
the following function will be handy:
\begin{minted}{haskell}
replicate :: SNat n -> a -> Vec n a

replicate d512 0 :: Vec 512 (BitVector 32) 
-- ^ create zero-filled vector
\end{minted}
\subsection{Remarks on Haskell Types}
Other Haskell types like \mintinline{haskell}{Maybe} can also be used without problem. For example, \mintinline{haskell}{Maybe Bool} will be represented by 2 bits in Verilog.
\begin{minted}{Haskell}
Just True  = 11
Just False = 10
Nothing    = 0x
\end{minted}
Clash will also find a way to represent your own defined sum types, for example,
\begin{minted}{Haskell}
data MyEnum = A | B | C | D
\end{minted}
will be represented by something like
\begin{minted}{Haskell}
A -> 00
B -> 01
C -> 10
D -> 11
\end{minted}

\section{Step into the Sequential Logic World} 
The previous parts are mainly talking about combinatorial logic; how about the sequential one? 

In Clash, sequential logic things are wrapped into a type \mintinline{haskell}{Signal (dom :: Symbol) a}.
The \mintinline{haskell}{dom} stands for the signal domain, which provides some basic configurations such as
clock, reset, enable, frequency and etc. The default domain is \mintinline{haskell}{System}, which is the standard global domain. It is also possible to define domains on your own and setup some multiple clock domains; these advanced features are not used in this book.

Signal is not Monad, but it provides the interfaces of Functor and Applicative.

To check the content of Signal, you can use a function called \mintinline{haskell}{sampleN}, to sample it.

To test your code within Signal environment, you can also use \mintinline{haskell}{enableGen} to generate enable signals, 
\mintinline{haskell}{clockGen} to generate clock signals and \mintinline{haskell}{resetGen} to generate reset signals. What's more, these is also a \mintinline{haskell}{simulate} function, which allows you to use a syntax like \mint{haskell}{simulate @System myInterface} to generate the result.

Usually, enable, clock and reset signals are required everywhere within a synchronous circuit. Image writing these three signals repeatedly at every function, it will definitely become tedious. Hence, Clash provides a special way to define a generalized signal domain which hides some global signals, you can simply expose them at those interfaces to synthesize.
The following example illustrates how to use this feature
\begin{minted}[breaklines, linenos]{haskell}
example :: HiddenClockResetEnable dom
    => Signal dom Bool
    -> Signal dom Bool
example = fmap complement

example' 
    :: Clock System
    -> Reset System
    -> Enable System
    -> Signal System Bool
    -> Signal System Bool
example' = exposeClockResetEnable example 
\end{minted} 

\section{Function Utilities}
\subsection{ROM and RAM}
Clash provides some predefined functions for us to declare large blocks of memory. 
\subsubsection{Asynchronous Memory}
Let us first have a look at the asynchronous ROM and RAM,
\begin{minted}{haskell}
asyncRom :: (KnownNat n, Enum addr) 
  => Vec n a  -- ^ initial vector 
  -> addr     -- ^ read address
  -> a        -- ^ read result         

asyncRam
  :: (  Enum addr, KnownDomain dom
     ,  GHC.Classes.IP (AppendSymbol dom "_clk") (Clock dom)
     ,  GHC.Classes.IP (AppendSymbol dom "_en") (Enable dom)) => SNat n                        -- ^ size
     -> Signal dom addr               -- ^ read address
     -> Signal dom (Maybe (addr, a))  -- ^ write data
     -> Signal dom a                  -- ^ read result
\end{minted}
The asynchronous version will output the content in the read address at the same clock cycle. If the read address and the write address conflicts, the default strategy is write-after-read; however, you can use \mintinline{haskell}{readNew . asyncRam} to apply the read-after-write strategy.

\subsubsection{Synchronous Memory}
Asynchronous memory is handy enough, but it is not the optimal structure: asynchronous memory may require a lot of LUTs in FPGA and the cost must be considered if the memory size if relatively large. Fortunately, there is a synchronous version of RAM, it corresponds to the BRAM structure in FPGA. However, there is a big difference that the read and write operation issued in the current clock cycle will generate outcome in the next cycle; we must take care of this feature when designing circuits.
\begin{minted}{haskell}
blockRam
  :: (  KnownDomain dom
     ,  GHC.Classes.IP (AppendSymbol dom "_clk") (Clock dom)
     ,  GHC.Classes.IP (AppendSymbol dom "_en") (Enable dom), NFDataX a
     ,  Enum addr ) 
     => Vec n a
     -> Signal dom addr 
     -> Signal dom (Maybe (addr, a)) 
     -> Signal dom a
\end{minted}

Similarly, you can change the read-write conflict resolution. 

For asynchronous ROM and synchronous RAM, Clash also provides some functions like \mintinline{haskell}{blockRamFile}, which will be translated into some Verilog code using \mintinline{verilog}{readmemb} function; which makes it possible for us to initialize the memory field using external files.

\subsection{Stateful Procedures}
There are several ways to handle stateful procedures.
\subsubsection{Register}
Clash provides a \mintinline{haskell}{register} function to help us to handle some basic stateful procedures. Register buffers and delays the input signal by one clock tick before output, and in the first clock tick the output is the initial value.
\begin{minted}{haskell}
register
  :: (  HiddenClockResetEnable dom
     ,  NFDataX a ) 
     => a 
     -> Signal dom a 
     -> Signal dom a

register 1 -- ^ declare a register with initial value 1
\end{minted}
\subsubsection{Mealy}
Mealy Machine is a sort of state machine whose output is determined by the current input and state.
\begin{minted}{haskell}
mealy
  :: (  HiddenClockResetEnable dom
     ,  NFDataX s ) 
     => (s -> i -> (s, o)) 
     -> s 
     -> Signal dom i 
     -> Signal dom o
\end{minted}
Let us write a special counter using Mealy Machine: if the outside provides an input, it will set it as the next counting value, otherwise, it just increases the counter and outputs the current value.

It seems that we can describe the state transformation with the following function:
\begin{minted}[breaklines]{haskell}
counterT 
  :: Unsigned 32 
  -> Maybe (Unsigned 32) 
  -> (Unsigned 32, Unsigned 32)
counterT state Nothing  = (state + 1, state)
counterT state (Just x) = (x        , state)
\end{minted}
To transform \mintinline{haskell}{counterT} into a state machine, just apply the \mintinline{haskell}{mealy} function together with an initial value to it
\begin{minted}[breaklines]{haskell}
counter 
  :: HiddenClockResetEnable dom
  => Signal dom (Maybe (Unsigned 32))
  => Signal dom (Unsigned 32)
counter = mealy counterT 0
\end{minted}
\subsubsection{Moore}
Another kind of commonly used finite state machine is the Moore Machine, whose output is determined only by the current state.
\begin{minted}{haskell}
moore
  :: ( HiddenClockResetEnable dom
     , NFDataX s )
     => (s -> i -> s)
     -> (s -> o)
     -> s
     -> (Signal dom i -> Signal dom o)
\end{minted}
The counter example can also be transformed into a Moore Machine:
\begin{minted}[breaklines]{haskell}
counterT 
  :: Unsigned 32 
  -> Maybe (Unsigned 32) 
  -> Unsigned 32
counterT state Nothing  = state + 1
counterT _     (Just x) = x

counter 
  :: HiddenClockResetEnable dom
  => Signal dom (Maybe (Unsigned 32))
  -> Signal dom (Unsigned 32)
counter = moore counterT id 0
\end{minted}
\subsubsection{State Monad}
It is also possible to use State Monad to implement the state machine.
Still use the counter as an example:
\begin{minted}[breaklines]{haskell}
counterS 
    :: Maybe (Unsigned 32)
    -> State (Unsigned 32) (Unsigned 32)
counterS input = do
    state <- get
    put $ maybe (state + 1) id input
    return state
\end{minted}
However, we need to write a function to transform our state monad into a synthesizable state machine:
\begin{minted}[breaklines]{haskell}
asStateM 
  :: (HiddenClockResetEnable dom, NFDataX s)
  => (i -> State s o)
  -> s
  -> (Signal dom i -> Signal dom o)
asStateM f i = mealy g i
  where
    g s x =
      let (o, s') = runState (f x) s
      in (s', o)
\end{minted}
With this function, we are able to write something like: \mint{haskell}{asStateM counterS 0}
\subsection{Bundle and Unbundle}
Consider we have the following functions
\begin{minted}[breaklines]{haskell}
foo
  :: HiddenClockResetEnable dom
  => Signal dom Input
  -> Signal dom (TypeA, TypeB)

bar
  :: HiddenClockResetEnable dom
  => Signal dom TypeA
  -> Signal dom TypeB
  -> Signal dom Result
\end{minted}
The problem emerges when we want to combine these two functions: it is hard to take out the value of each part of the tuple wrapped in the signal environment.

Hence, we can use the \mintinline{haskell}{unbundle} function.
\begin{minted}{haskell}
let (a, b) = unbundle $ foo input
in bar a b
\end{minted}
The \mintinline{haskell}{bundle} function just acts as the inverse of \mintinline{haskell}{unbundle}:
\begin{minted}{haskell}
a :: Signal System A
b :: Signal System B
func :: Signal System (A, B) -> Signal System C
res  :: Signal System C
res  =  func $ bundle (a, b)
\end{minted}